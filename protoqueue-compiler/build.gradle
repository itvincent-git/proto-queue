apply plugin: 'java-library'
apply plugin: 'kotlin'

ext {
    artifact_id = 'protoqueue-compiler'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':protoqueue-common')
    //implementation deps.javapoet
    implementation deps.autocommon
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutines
    implementation deps.kotlinpoet
}

apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'signing'
apply plugin: 'net.researchgate.release'
release {
    failOnUnversionedFiles = false
    failOnCommitNeeded = false
    failOnUpdateNeeded = false
    failOnPublishNeeded = false
    failOnSnapshotDependencies = false
    buildTasks = ['publish'] ////定义发布时该执行哪个task
    versionPropertyFile = '../gradle.properties'
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava ///若包含图片等资源改为allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "$group_id"
            artifactId "$artifact_id"
            version "$release_version"
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username = getRepositoryUsername()
                password = getRepositoryPassword()
            }

            if(isReleaseBuild()) {
                url getReleaseRepositoryUrl()
            } else {
                url getSnapshotRepositoryUrl()
            }
        }
    }
}